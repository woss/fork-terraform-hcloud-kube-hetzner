resource "hcloud_primary_ip" "control_planes_ipv4" {
  for_each = {
    for key, value in local.control_plane_nodes : key => value
    if var.primary_ip_pool.enable_ipv4 && !value.disable_ipv4 && value.primary_ipv4_id == null
  }

  type        = "ipv4"
  name        = "${var.cluster_name}-cp-${each.key}-ipv4"
  location    = each.value.location
  auto_delete = var.primary_ip_pool.auto_delete

  lifecycle {
    ignore_changes = [location]
  }
}

resource "hcloud_primary_ip" "control_planes_ipv6" {
  for_each = {
    for key, value in local.control_plane_nodes : key => value
    if var.primary_ip_pool.enable_ipv6 && !value.disable_ipv6 && value.primary_ipv6_id == null
  }

  type        = "ipv6"
  name        = "${var.cluster_name}-cp-${each.key}-ipv6"
  location    = each.value.location
  auto_delete = var.primary_ip_pool.auto_delete

  lifecycle {
    ignore_changes = [location]
  }
}

module "control_planes" {
  source = "./modules/host"

  providers = {
    hcloud = hcloud,
  }

  for_each = local.control_plane_nodes

  name                          = "${var.use_cluster_name_in_node_name ? "${var.cluster_name}-" : ""}${each.value.nodepool_name}"
  append_random_suffix          = each.value.append_random_suffix
  connection_host               = ""
  connection_host_suffix        = local.tailscale_pre_terraform_ssh_enabled ? local.tailscale_magicdns_domain : ""
  os_snapshot_id                = try(trimspace(each.value.os_snapshot_id), "") != "" ? trimspace(each.value.os_snapshot_id) : local.snapshot_id_by_os[each.value.os][substr(each.value.server_type, 0, 3) == "cax" ? "arm" : "x86"]
  os                            = each.value.os
  base_domain                   = var.base_domain
  ssh_keys                      = length(var.ssh_hcloud_key_label) > 0 ? concat([local.hcloud_ssh_key_id], data.hcloud_ssh_keys.keys_by_selector[0].ssh_keys.*.id) : [local.hcloud_ssh_key_id]
  ssh_port                      = var.ssh_port
  ssh_public_key                = local.ssh_public_key
  ssh_private_key               = var.ssh_private_key
  ssh_additional_public_keys    = length(var.ssh_hcloud_key_label) > 0 ? concat(local.ssh_additional_public_keys, [for key in data.hcloud_ssh_keys.keys_by_selector[0].ssh_keys.*.public_key : trimspace(key)]) : local.ssh_additional_public_keys
  ssh_authorized_keys_exclusive = var.ssh_authorized_keys_exclusive
  firewall_ids                  = each.value.disable_ipv4 && each.value.disable_ipv6 ? [] : [hcloud_firewall.k3s.id] # Cannot attach a firewall when public interfaces are disabled
  extra_firewall_ids            = each.value.disable_ipv4 && each.value.disable_ipv6 ? [] : var.extra_firewall_ids
  placement_group_id            = var.enable_placement_groups ? (each.value.placement_group == null ? hcloud_placement_group.control_plane[each.value.placement_group_index].id : hcloud_placement_group.control_plane_named[each.value.placement_group].id) : null
  location                      = each.value.location
  server_type                   = each.value.server_type
  backups                       = each.value.backups
  ipv4_subnet_id                = hcloud_network_subnet.control_plane[local.use_per_nodepool_subnets ? [for i, v in var.control_plane_nodepools : i if v.name == each.value.nodepool_name][0] : 0].id
  dns_servers                   = var.dns_servers
  registries_config             = local.registries_config_effective
  registries_update_script      = local.k8s_registries_update_script
  kubelet_config                = var.kubelet_config
  kubelet_config_update_script  = local.k8s_kubelet_config_update_script
  audit_policy_config           = var.audit_policy_config
  audit_policy_update_script    = local.k3s_audit_policy_update_script
  cloudinit_write_files_common  = local.cloudinit_write_files_common
  metadata_route_repair_script  = local.metadata_route_repair_script
  cloudinit_runcmd_common       = local.cloudinit_runcmd_common
  cloudinit_write_files_extra   = concat(each.value.extra_write_files, local.node_annotation_write_files_by_scope["control-plane:${each.key}"])
  cloudinit_runcmd_extra        = concat(local.tailscale_cloud_init_bootstrap_enabled ? [local.tailscale_bootstrap_script_static_control_plane_by_node[each.key]] : [], each.value.extra_runcmd, length(each.value.annotations) == 0 ? [] : local.node_annotations_enable_runcmd)
  swap_size                     = each.value.swap_size
  zram_size                     = each.value.zram_size
  keep_disk_size                = coalesce(each.value.keep_disk, var.keep_disk_control_plane_nodes)
  disable_ipv4                  = each.value.disable_ipv4
  disable_ipv6                  = each.value.disable_ipv6
  primary_ipv4_id               = each.value.primary_ipv4_id != null ? each.value.primary_ipv4_id : try(hcloud_primary_ip.control_planes_ipv4[each.key].id, null)
  primary_ipv6_id               = each.value.primary_ipv6_id != null ? each.value.primary_ipv6_id : try(hcloud_primary_ip.control_planes_ipv6[each.key].id, null)
  ssh_bastion                   = local.ssh_bastion
  node_connection_overrides     = var.node_connection_overrides
  network_id                    = local.control_plane_primary_network_id_by_node[each.key]
  primary_network_key           = each.value.network_id
  extra_network_ids             = local.control_plane_effective_extra_network_ids_by_node[each.key]

  # We leave some room so 100 eventual Hetzner LBs that can be created perfectly safely
  # It leaves the subnet with 254 x 254 - 100 = 64416 IPs to use, so probably enough.
  private_ipv4 = null

  labels = merge(local.labels, local.labels_control_plane_node, each.value.hcloud_labels, { "kube-hetzner/os" = each.value.os })

  automatically_upgrade_os = var.automatically_upgrade_os

  network_gw_ipv4 = local.network_gw_ipv4_by_network_id[local.control_plane_primary_network_id_by_node[each.key]]

  depends_on = [
    hcloud_network_subnet.control_plane,
    hcloud_placement_group.control_plane,
    hcloud_server.nat_router,
    terraform_data.nat_router_await_cloud_init,
    terraform_data.nat_router_fail2ban,
    terraform_data.nat_router_extra_runcmd,
  ]
}

resource "hcloud_floating_ip" "control_planes" {
  for_each = {
    for k, v in local.control_plane_nodes : k => v
    if coalesce(lookup(v, "floating_ip"), false) && lookup(v, "floating_ip_id", null) == null
  }

  type              = "ipv4"
  labels            = local.labels
  home_location     = each.value.location
  delete_protection = var.enable_delete_protection.floating_ip
}

data "hcloud_floating_ip" "control_planes_existing" {
  for_each = {
    for k, v in local.control_plane_nodes : k => v
    if coalesce(lookup(v, "floating_ip"), false) && lookup(v, "floating_ip_id", null) != null
  }

  id = each.value.floating_ip_id
}

resource "hcloud_floating_ip_assignment" "control_planes" {
  for_each = {
    for k, v in local.control_plane_nodes : k => v
    if coalesce(lookup(v, "floating_ip"), false)
  }

  floating_ip_id = local.control_plane_floating_ip_id_by_node[each.key]
  server_id      = module.control_planes[each.key].id

  depends_on = [
    terraform_data.first_control_plane,
  ]
}

resource "terraform_data" "configure_control_plane_floating_ip" {
  for_each = {
    for k, v in local.control_plane_nodes : k => v
    if coalesce(lookup(v, "floating_ip"), false)
  }

  triggers_replace = {
    control_plane_id = module.control_planes[each.key].id
    floating_ip_id   = local.control_plane_floating_ip_id_by_node[each.key]
  }

  provisioner "remote-exec" {
    inline = [
      <<-EOT
	      route_dev() {
	          awk '{for(i=1;i<=NF;i++) if($i=="dev"){print $(i+1); exit}}'
	      }

	      PRIV_IF=$(ip -4 route show ${local.network_ipv4_cidr_by_network_id[local.control_plane_primary_network_id_by_node[each.key]]} 2>/dev/null | route_dev)
	      ETH=$(ip -4 route get 172.31.1.1 2>/dev/null | route_dev)
	      if [ -n "$PRIV_IF" ] && [ "$ETH" = "$PRIV_IF" ]; then
	          ETH=""
	      fi
	      if [ -z "$ETH" ]; then
	          ETH=$(ip -o -4 addr show scope global 2>/dev/null | awk -v priv="$PRIV_IF" '$2 != priv {print $2; exit}')
	      fi
	      if [ -z "$ETH" ]; then
	          ETH=$(ip -o link show up 2>/dev/null | awk -F': ' -v priv="$PRIV_IF" '$2 != "lo" && $2 != priv {print $2; exit}')
	      fi
	      if [ -z "$ETH" ]; then
	          echo "ERROR: Could not detect public interface for floating IP configuration" >&2
	          exit 1
	      fi

	      NM_CONNECTION=$(nmcli -g GENERAL.CONNECTION device show "$ETH" 2>/dev/null | head -1)
	      if [ -z "$NM_CONNECTION" ]; then
	          echo "ERROR: No NetworkManager connection found for $ETH" >&2
	          exit 1
      fi

      nmcli connection modify "$NM_CONNECTION" \
          ipv4.method manual \
          ipv4.addresses ${local.control_plane_external_ipv4_by_node[each.key]}/32,${module.control_planes[each.key].ipv4_address != null && module.control_planes[each.key].ipv4_address != "" ? module.control_planes[each.key].ipv4_address : module.control_planes[each.key].private_ipv4_address}/32 gw4 172.31.1.1 \
          ipv4.route-metric 100 \
      && nmcli connection up "$NM_CONNECTION"
      EOT
    ]
  }

  connection {
    user           = "root"
    private_key    = var.ssh_private_key
    agent_identity = local.ssh_agent_identity
    host           = local.control_plane_ips[each.key]
    port           = var.ssh_port

    bastion_host        = local.ssh_bastion.bastion_host
    bastion_port        = local.ssh_bastion.bastion_port
    bastion_user        = local.ssh_bastion.bastion_user
    bastion_private_key = local.ssh_bastion.bastion_private_key
  }

  depends_on = [
    hcloud_floating_ip_assignment.control_planes
  ]
}

resource "hcloud_load_balancer" "control_plane" {
  count = var.enable_control_plane_load_balancer ? 1 : 0
  name  = "${var.cluster_name}-control-plane"

  load_balancer_type = var.control_plane_load_balancer_type
  location           = var.load_balancer_location
  labels             = merge(local.labels, local.labels_control_plane_lb)
  delete_protection  = var.enable_delete_protection.load_balancer

  lifecycle {
    ignore_changes = [location]
  }
}

resource "hcloud_load_balancer_network" "control_plane" {
  count = var.enable_control_plane_load_balancer ? 1 : 0

  load_balancer_id        = hcloud_load_balancer.control_plane.*.id[0]
  subnet_id               = hcloud_network_subnet.control_plane.*.id[0]
  enable_public_interface = var.control_plane_load_balancer_enable_public_network
  ip                      = cidrhost(hcloud_network_subnet.control_plane.*.ip_range[0], -2)

  # Keep existing LB IPs stable on upgrade.
  lifecycle {
    ignore_changes = [ip]
  }
}

resource "hcloud_load_balancer_target" "control_plane" {
  count = var.enable_control_plane_load_balancer ? 1 : 0

  depends_on       = [hcloud_load_balancer_network.control_plane]
  type             = "label_selector"
  load_balancer_id = hcloud_load_balancer.control_plane.*.id[0]
  label_selector   = join(",", [for k, v in merge(local.labels, local.labels_control_plane_node) : "${k}=${v}"])
  use_private_ip   = !local.multinetwork_overlay_enabled
}

resource "hcloud_load_balancer_service" "control_plane" {
  count = var.enable_control_plane_load_balancer ? 1 : 0

  depends_on = [
    hcloud_load_balancer_network.control_plane,
    hcloud_load_balancer_target.control_plane,
  ]

  load_balancer_id = hcloud_load_balancer.control_plane.*.id[0]
  protocol         = "tcp"
  # Keep the LB backend aligned with the configured API listener port.
  destination_port = var.kubernetes_api_port
  listen_port      = var.kubernetes_api_port

  health_check {
    protocol = "http"
    port     = var.kubernetes_api_port
    interval = tonumber(trimsuffix(var.load_balancer_health_check_interval, "s"))
    timeout  = tonumber(trimsuffix(var.load_balancer_health_check_timeout, "s"))
    retries  = var.load_balancer_health_check_retries

    http {
      path         = "/readyz"
      tls          = true
      status_codes = ["200", "401"]
    }
  }
}

# RKE2 node registration uses the supervisor port (9345). When the control-plane
# endpoint is fronted by a load balancer, this extra service is required for
# agents/control-planes that join via the LB private IP.
resource "hcloud_load_balancer_service" "control_plane_rke2_supervisor" {
  count = (var.enable_control_plane_load_balancer && local.kubernetes_distribution == "rke2" && var.kubernetes_api_port != 9345) ? 1 : 0

  depends_on = [
    hcloud_load_balancer_network.control_plane,
    hcloud_load_balancer_target.control_plane,
  ]

  load_balancer_id = hcloud_load_balancer.control_plane.*.id[0]
  protocol         = "tcp"
  destination_port = 9345
  listen_port      = 9345
}

resource "hcloud_rdns" "control_plane_lb_ipv4" {
  count = (var.enable_control_plane_load_balancer && var.control_plane_load_balancer_enable_public_network && var.base_domain != "") ? 1 : 0

  load_balancer_id = hcloud_load_balancer.control_plane[0].id
  ip_address       = hcloud_load_balancer.control_plane[0].ipv4
  dns_ptr          = "${var.cluster_name}-control-plane.${var.base_domain}"
}

locals {
  control_plane_floating_ip_id_by_node = {
    for k, v in local.control_plane_nodes :
    k => coalesce(
      try(data.hcloud_floating_ip.control_planes_existing[k].id, null),
      try(hcloud_floating_ip.control_planes[k].id, null),
    )
    if coalesce(lookup(v, "floating_ip"), false)
  }

  control_plane_external_ipv4_by_node = {
    for k, v in local.control_plane_nodes :
    k => coalesce(
      try(data.hcloud_floating_ip.control_planes_existing[k].ip_address, null),
      try(hcloud_floating_ip.control_planes[k].ip_address, null),
    )
    if coalesce(lookup(v, "floating_ip"), false)
  }

  control_plane_override_base_names = {
    for k, v in local.control_plane_nodes :
    k => "${var.use_cluster_name_in_node_name ? "${var.cluster_name}-" : ""}${v.nodepool_name}"
  }

  tailscale_control_plane_magicdns_hosts = {
    for k, v in module.control_planes :
    k => "${v.name}.${local.tailscale_magicdns_domain}"
  }

  control_plane_initial_ips = {
    for k, v in module.control_planes : k => coalesce(
      lookup(var.node_connection_overrides, v.name, null),
      lookup(var.node_connection_overrides, local.control_plane_override_base_names[k], null),
      v.ipv4_address,
      v.ipv6_address,
      v.private_ipv4_address
    )
  }

  control_plane_ips = {
    for k, v in module.control_planes : k => coalesce(
      lookup(var.node_connection_overrides, v.name, null),
      lookup(var.node_connection_overrides, local.control_plane_override_base_names[k], null),
      local.tailscale_use_tailnet_for_terraform ? local.tailscale_control_plane_magicdns_hosts[k] : null,
      v.ipv4_address,
      v.ipv6_address,
      v.private_ipv4_address
    )
  }

  attached_control_plane_volumes = merge([
    for node_key, node in local.control_plane_nodes : {
      for volume_idx, volume in coalesce(node.attached_volumes, []) :
      "${node_key}-${volume_idx}" => {
        node_key          = node_key
        volume_idx        = volume_idx
        size              = volume.size
        mount_path        = volume.mount_path
        filesystem        = volume.filesystem
        automount         = volume.automount
        name              = volume.name
        labels            = volume.labels
        delete_protection = volume.delete_protection
      }
    }
  ]...)

  rke2-config = { for k, v in local.control_plane_nodes : k => merge(
    {
      node-name = module.control_planes[k].name
      server = (
        length(module.control_planes) == 1 ? null :
        module.control_planes[k].private_ipv4_address == module.control_planes[keys(module.control_planes)[0]].private_ipv4_address ? null :
        local.rke2_control_plane_join_endpoint_by_node[k]
      )
      token                       = local.cluster_token
      disable-cloud-controller    = true
      disable-kube-proxy          = !var.enable_kube_proxy
      disable                     = local.disable_rke2_extras
      kubelet-arg                 = local.control_plane_effective_kubelet_args_by_node[k]
      kube-apiserver-arg          = concat(local.kube_apiserver_arg, var.enable_secrets_encryption ? ["encryption-provider-config=${local.secrets_encryption_config_file}"] : [])
      kube-controller-manager-arg = local.kube_controller_manager_arg
      node-ip                     = local.multinetwork_overlay_enabled ? local.control_plane_public_overlay_node_ip_by_node[k] : local.control_plane_node_ip_by_node[k]
      advertise-address           = module.control_planes[k].private_ipv4_address
      node-label                  = v.labels
      node-taint                  = v.taints
      selinux                     = !var.enable_selinux ? false : (v.selinux == true ? true : false)
      cluster-cidr                = local.cluster_cidr
      service-cidr                = local.service_cidr
      cluster-dns                 = local.cluster_dns
      write-kubeconfig-mode       = "0644" # needed for import into rancher
      cni                         = local.rke2_cni
    },
    local.multinetwork_overlay_enabled ? {
      node-external-ip = join(",", compact([local.multinetwork_transport_ipv4_enabled ? module.control_planes[k].ipv4_address : null, local.multinetwork_transport_ipv6_enabled ? module.control_planes[k].ipv6_address : null]))
      } : lookup(local.control_plane_external_ipv4_by_node, k, null) != null ? {
      node-external-ip = local.control_plane_external_ipv4_by_node[k]
    } : {},
    local.embedded_registry_mirror_server_config,
    local.disable_default_registry_endpoint_config,
    var.enable_control_plane_load_balancer ? {
      tls-san = concat(
        compact([
          hcloud_load_balancer.control_plane.*.ipv4[0],
          hcloud_load_balancer_network.control_plane.*.ip[0],
          local.kubeconfig_server_address != "" ? local.kubeconfig_server_address : null,
          local.control_plane_endpoint_host,
          !var.control_plane_load_balancer_enable_public_network && var.nat_router != null ? hcloud_server.nat_router[0].ipv4_address : null
        ]),
        var.additional_tls_sans
      )
      } : {
      tls-san = concat(
        compact([
          module.control_planes[keys(module.control_planes)[0]].private_ipv4_address != "" ? module.control_planes[keys(module.control_planes)[0]].private_ipv4_address : null,
          module.control_planes[k].ipv4_address != "" ? module.control_planes[k].ipv4_address : null,
          module.control_planes[k].ipv6_address != "" ? module.control_planes[k].ipv6_address : null,
          local.control_plane_endpoint_host,
          local.kubeconfig_server_address != "" ? local.kubeconfig_server_address : null,
          try(one(module.control_planes[k].network).ip, null)
        ]),
      var.additional_tls_sans)
    },
    local.etcd_s3_snapshots,
    var.control_planes_custom_config
  ) }

  k3s-config = { for k, v in local.control_plane_nodes : k => merge(
    {
      node-name                = module.control_planes[k].name
      server                   = length(module.control_planes) == 1 ? null : local.k3s_control_plane_join_endpoint_by_node[k]
      token                    = local.cluster_token
      disable-cloud-controller = true
      disable-kube-proxy       = !var.enable_kube_proxy
      disable                  = local.disable_extras
      https-listen-port        = var.kubernetes_api_port
      # Kubelet arg precedence (last wins): local.kubelet_arg < global_kubelet_args < control_plane_kubelet_args < v.kubelet_args
      kubelet-arg                 = local.control_plane_effective_kubelet_args_by_node[k]
      kube-apiserver-arg          = concat(local.kube_apiserver_arg, var.enable_secrets_encryption ? ["encryption-provider-config=${local.secrets_encryption_config_file}"] : [])
      kube-controller-manager-arg = local.kube_controller_manager_arg
      flannel-iface               = local.flannel_iface
      node-ip                     = local.multinetwork_overlay_enabled ? local.control_plane_public_overlay_node_ip_by_node[k] : local.control_plane_node_ip_by_node[k]
      advertise-address           = module.control_planes[k].private_ipv4_address
      node-label                  = v.labels
      node-taint                  = v.taints
      selinux                     = !var.enable_selinux ? false : (v.selinux == true ? true : false)
      cluster-cidr                = local.cluster_cidr
      service-cidr                = local.service_cidr
      cluster-dns                 = local.cluster_dns
      write-kubeconfig-mode       = "0644" # needed for import into rancher
    },
    local.multinetwork_overlay_enabled ? {
      node-external-ip     = join(",", compact([local.multinetwork_transport_ipv4_enabled ? module.control_planes[k].ipv4_address : null, local.multinetwork_transport_ipv6_enabled ? module.control_planes[k].ipv6_address : null]))
      egress-selector-mode = "disabled"
      } : lookup(local.control_plane_external_ipv4_by_node, k, null) != null ? {
      node-external-ip    = local.control_plane_external_ipv4_by_node[k]
      flannel-external-ip = true
    } : {},
    lookup(local.cni_k3s_settings, var.cni_plugin, {}),
    local.embedded_registry_mirror_server_config,
    local.disable_default_registry_endpoint_config,
    var.enable_control_plane_load_balancer ? {
      tls-san = concat(
        compact([
          hcloud_load_balancer.control_plane.*.ipv4[0],
          hcloud_load_balancer_network.control_plane.*.ip[0],
          var.kubeconfig_server_address != "" ? var.kubeconfig_server_address : null,
          local.control_plane_endpoint_host,
          !var.control_plane_load_balancer_enable_public_network && var.nat_router != null ? hcloud_server.nat_router[0].ipv4_address : null
        ]),
        var.additional_tls_sans
      )
      } : {
      tls-san = concat(
        compact([
          local.control_plane_endpoint_host,
          module.control_planes[k].ipv4_address != "" ? module.control_planes[k].ipv4_address : null,
          module.control_planes[k].ipv6_address != "" ? module.control_planes[k].ipv6_address : null,
          var.kubeconfig_server_address != "" ? var.kubeconfig_server_address : null,
          try(one(module.control_planes[k].network).ip, null)
        ]),
      var.additional_tls_sans)
    },
    local.etcd_s3_snapshots,
    var.control_planes_custom_config,
    local.prefer_bundled_bin_config
  ) }
}

resource "terraform_data" "control_plane_config_rke2" {
  for_each = local.kubernetes_distribution == "rke2" ? local.control_plane_nodes : {}

  triggers_replace = {
    control_plane_id = module.control_planes[each.key].id
    config           = sha1(yamlencode(local.rke2-config[each.key]))
    cni_values       = sha1(local.desired_cni_values)
    encryption       = sha1(local.secrets_encryption_config)
    encryption_guard = "explicit-desired-state-v2"
  }

  connection {
    user           = "root"
    private_key    = var.ssh_private_key
    agent_identity = local.ssh_agent_identity
    host           = local.control_plane_ips[each.key]
    port           = var.ssh_port

    bastion_host        = local.ssh_bastion.bastion_host
    bastion_port        = local.ssh_bastion.bastion_port
    bastion_user        = local.ssh_bastion.bastion_user
    bastion_private_key = local.ssh_bastion.bastion_private_key
  }

  # Generating k8s server config file
  provisioner "file" {
    content     = yamlencode(local.rke2-config[each.key])
    destination = "/tmp/config.yaml"
  }

  provisioner "file" {
    content     = local.secrets_encryption_config
    destination = local.secrets_encryption_staging_file
  }

  # Create /var/lib/rancher/rke2/server/manifests directory
  provisioner "remote-exec" {
    inline = [
      "mkdir -p /var/lib/rancher/rke2/server/manifests/",
    ]
  }

  # Gateway API CRDs must exist before Cilium starts when Cilium Gateway API is enabled.
  provisioner "file" {
    content     = local.gateway_api_standard_crds_file
    destination = "/var/lib/rancher/rke2/server/manifests/00-gateway-api-standard-crds.yaml"
  }

  # Upload the CNI install file.
  provisioner "file" {
    content = templatefile(
      "${path.module}/templates/${local.rke2_manifest_cni_plugin}.yaml.tpl",
      {
        values  = local.rke2_manifest_cni_plugin == "cilium" ? indent(4, trimspace(local.desired_cni_values)) : ""
        version = local.desired_cni_version
    })
    destination = "/var/lib/rancher/rke2/server/manifests/${local.rke2_manifest_cni_plugin}.yaml"
  }

  # Upload bundled RKE2 CNI HelmChartConfig overrides.
  provisioner "file" {
    content     = local.rke2_cni_config_manifest
    destination = "/var/lib/rancher/rke2/server/manifests/kube-hetzner-rke2-cni-config.yaml"
  }

  provisioner "remote-exec" {
    inline = [local.k8s_config_update_script]
  }

  depends_on = [
    terraform_data.first_control_plane_rke2,
    hcloud_network_subnet.control_plane,
    terraform_data.tailscale_control_planes,
  ]
}
moved {
  from = null_resource.control_plane_config_rke2
  to   = terraform_data.control_plane_config_rke2
}

resource "terraform_data" "control_plane_config" {
  for_each = local.kubernetes_distribution == "k3s" ? local.control_plane_nodes : {}

  triggers_replace = {
    control_plane_id = module.control_planes[each.key].id
    config           = sha1(yamlencode(local.k3s-config[each.key]))
    encryption       = sha1(local.secrets_encryption_config)
    encryption_guard = "explicit-desired-state-v2"
  }

  connection {
    user           = "root"
    private_key    = var.ssh_private_key
    agent_identity = local.ssh_agent_identity
    host           = local.control_plane_ips[each.key]
    port           = var.ssh_port

    bastion_host        = local.ssh_bastion.bastion_host
    bastion_port        = local.ssh_bastion.bastion_port
    bastion_user        = local.ssh_bastion.bastion_user
    bastion_private_key = local.ssh_bastion.bastion_private_key

  }

  # Generating k8s server config file
  provisioner "file" {
    content     = yamlencode(local.k3s-config[each.key])
    destination = "/tmp/config.yaml"
  }

  provisioner "file" {
    content     = local.secrets_encryption_config
    destination = local.secrets_encryption_staging_file
  }

  provisioner "remote-exec" {
    inline = [local.k3s_config_update_script]
  }

  depends_on = [
    terraform_data.first_control_plane,
    hcloud_network_subnet.control_plane,
    terraform_data.tailscale_control_planes,
  ]
}
moved {
  from = null_resource.control_plane_config
  to   = terraform_data.control_plane_config
}

resource "terraform_data" "audit_policy" {
  for_each = local.control_plane_nodes

  triggers_replace = {
    control_plane_id = module.control_planes[each.key].id
    audit_policy     = sha1(var.audit_policy_config)
  }

  connection {
    user           = "root"
    private_key    = var.ssh_private_key
    agent_identity = local.ssh_agent_identity
    host           = local.control_plane_ips[each.key]
    port           = var.ssh_port

    bastion_host        = local.ssh_bastion.bastion_host
    bastion_port        = local.ssh_bastion.bastion_port
    bastion_user        = local.ssh_bastion.bastion_user
    bastion_private_key = local.ssh_bastion.bastion_private_key
  }

  provisioner "file" {
    content     = var.audit_policy_config
    destination = "/tmp/audit-policy.yaml"
  }

  provisioner "remote-exec" {
    inline = [local.k3s_audit_policy_update_script]
  }

  depends_on = [
    terraform_data.first_control_plane,
    hcloud_network_subnet.control_plane
  ]
}
moved {
  from = null_resource.audit_policy
  to   = terraform_data.audit_policy
}

resource "terraform_data" "authentication_config" {
  for_each = local.control_plane_nodes

  triggers_replace = {
    control_plane_id      = module.control_planes[each.key].id
    authentication_config = sha1(var.authentication_config)
  }

  connection {
    user           = "root"
    private_key    = var.ssh_private_key
    agent_identity = local.ssh_agent_identity
    host           = local.control_plane_ips[each.key]
    port           = var.ssh_port

    bastion_host        = local.ssh_bastion.bastion_host
    bastion_port        = local.ssh_bastion.bastion_port
    bastion_user        = local.ssh_bastion.bastion_user
    bastion_private_key = local.ssh_bastion.bastion_private_key

  }

  provisioner "file" {
    content     = var.authentication_config
    destination = "/tmp/authentication_config.yaml"
  }

  provisioner "remote-exec" {
    inline = [local.k8s_authentication_config_update_script]
  }

  depends_on = [
    terraform_data.first_control_plane,
    hcloud_network_subnet.control_plane
  ]
}
moved {
  from = null_resource.authentication_config
  to   = terraform_data.authentication_config
}

resource "terraform_data" "control_planes_rke2" {
  for_each = local.kubernetes_distribution == "rke2" ? local.control_plane_nodes : {}

  triggers_replace = {
    control_plane_id = module.control_planes[each.key].id
  }

  connection {
    user           = "root"
    private_key    = var.ssh_private_key
    agent_identity = local.ssh_agent_identity
    host           = local.control_plane_ips[each.key]
    port           = var.ssh_port

    bastion_host        = local.ssh_bastion.bastion_host
    bastion_port        = local.ssh_bastion.bastion_port
    bastion_user        = local.ssh_bastion.bastion_user
    bastion_private_key = local.ssh_bastion.bastion_private_key
  }

  # Install rke2 server
  provisioner "remote-exec" {
    inline = concat(local.k8s_install_network_env_by_control_plane[each.key], local.install_k8s_server)
  }

  # Start the server and wait until it is ready.
  provisioner "remote-exec" {
    inline = [
      "systemctl enable --now iscsid",
      "systemctl start rke2-server",
      "systemctl enable rke2-server",
      "mkdir -p /var/post_install /var/user_kustomize",
      <<-EOT
      timeout 360 bash <<EOF
        until systemctl status rke2-server > /dev/null; do
          systemctl start rke2-server
          echo "Waiting for the rke2 server to start..."
          sleep 3
        done
      EOF
      EOT
    ]
  }

  depends_on = [
    terraform_data.first_control_plane_rke2,
    terraform_data.control_plane_config_rke2,
    terraform_data.authentication_config,
    hcloud_load_balancer_service.control_plane,
    hcloud_load_balancer_service.control_plane_rke2_supervisor,
    hcloud_network_subnet.control_plane
  ]
}
moved {
  from = null_resource.control_planes_rke2
  to   = terraform_data.control_planes_rke2
}

resource "terraform_data" "control_planes" {
  for_each = local.kubernetes_distribution == "k3s" ? local.control_plane_nodes : {}

  triggers_replace = {
    control_plane_id = module.control_planes[each.key].id
  }

  connection {
    user           = "root"
    private_key    = var.ssh_private_key
    agent_identity = local.ssh_agent_identity
    host           = local.control_plane_ips[each.key]
    port           = var.ssh_port

    bastion_host        = local.ssh_bastion.bastion_host
    bastion_port        = local.ssh_bastion.bastion_port
    bastion_user        = local.ssh_bastion.bastion_user
    bastion_private_key = local.ssh_bastion.bastion_private_key

  }

  # Install k3s server
  provisioner "remote-exec" {
    inline = concat(local.k8s_install_network_env_by_control_plane[each.key], local.install_k3s_server)
  }

  # Start the server and wait until it is ready.
  provisioner "remote-exec" {
    inline = [
      "systemctl enable --now iscsid",
      "systemctl start k3s 2> /dev/null",
      "mkdir -p /var/post_install /var/user_kustomize",
      <<-EOT
      timeout 360 bash <<EOF
        until systemctl status k3s > /dev/null; do
          systemctl start k3s 2> /dev/null
          echo "Waiting for the k3s server to start..."
          sleep 3
        done
      EOF
      EOT
    ]
  }

  depends_on = [
    terraform_data.first_control_plane,
    terraform_data.control_plane_config,
    terraform_data.authentication_config,
    hcloud_load_balancer_service.control_plane,
    hcloud_network_subnet.control_plane
  ]
}
moved {
  from = null_resource.control_planes
  to   = terraform_data.control_planes
}

resource "hcloud_volume" "attached_control_plane_volume" {
  for_each = local.attached_control_plane_volumes

  labels = merge(
    {
      provisioner = "terraform"
      cluster     = var.cluster_name
      scope       = "attached-volume"
      role        = "control-plane"
    },
    each.value.labels
  )

  name              = coalesce(each.value.name, "${var.cluster_name}-cp-${module.control_planes[each.value.node_key].name}-vol-${each.value.volume_idx}")
  size              = each.value.size
  server_id         = module.control_planes[each.value.node_key].id
  automount         = each.value.automount
  format            = each.value.filesystem
  delete_protection = coalesce(each.value.delete_protection, var.enable_delete_protection.volume)
}

resource "terraform_data" "configure_attached_control_plane_volume" {
  for_each = local.attached_control_plane_volumes

  triggers_replace = {
    control_plane_id = module.control_planes[each.value.node_key].id
    volume_id        = hcloud_volume.attached_control_plane_volume[each.key].id
    volume_size      = hcloud_volume.attached_control_plane_volume[each.key].size
    mount_path       = each.value.mount_path
    filesystem       = each.value.filesystem
    volume_name      = hcloud_volume.attached_control_plane_volume[each.key].name
  }

  provisioner "remote-exec" {
    inline = [
      <<-EOT
      set -e
      systemctl enable --now iscsid

      device='${hcloud_volume.attached_control_plane_volume[each.key].linux_device}'
      mount_path='${each.value.mount_path}'
      fstype='${each.value.filesystem}'

      mkdir -p "$mount_path" >/dev/null
      uuid="$(blkid -s UUID -o value "$device")"
      if [ -z "$uuid" ]; then
        echo "Unable to determine filesystem UUID for $device" >&2
        exit 1
      fi

      if mountpoint -q "$mount_path"; then
        mounted_source="$(findmnt -rn -T "$mount_path" -o SOURCE)"
        mounted_uuid="$(blkid -s UUID -o value "$mounted_source" 2>/dev/null || true)"
        if [ "$mounted_uuid" != "$uuid" ]; then
          umount "$mount_path"
        fi
      fi

      mountpoint -q "$mount_path" || mount -o discard,defaults "$device" "$mount_path"

      case "$fstype" in
        ext4) resize2fs "$device" ;;
        xfs) xfs_growfs "$mount_path" ;;
        *) echo "Unsupported attached volume filesystem type: $fstype" >&2; exit 1 ;;
      esac

      tmp_fstab="$(mktemp)"
      awk -v path="$mount_path" -v uuid="$uuid" -v device="$device" '$0 ~ /^#/ || ($1 != "UUID=" uuid && $1 != device && $2 != path) { print }' /etc/fstab > "$tmp_fstab"
      cat "$tmp_fstab" > /etc/fstab
      rm -f "$tmp_fstab"
      printf 'UUID=%s %s %s discard,nofail,defaults 0 0\n' "$uuid" "$mount_path" "$fstype" >> /etc/fstab
      EOT
    ]
  }

  connection {
    user           = "root"
    private_key    = var.ssh_private_key
    agent_identity = local.ssh_agent_identity
    host           = local.control_plane_ips[each.value.node_key]
    port           = var.ssh_port

    bastion_host        = local.ssh_bastion.bastion_host
    bastion_port        = local.ssh_bastion.bastion_port
    bastion_user        = local.ssh_bastion.bastion_user
    bastion_private_key = local.ssh_bastion.bastion_private_key
  }

  depends_on = [
    hcloud_volume.attached_control_plane_volume
  ]
}
